#!/usr/bin/env perl
use strict;
use warnings;
use Term::ReadKey;
use Device::SerialPort;
use Switch;
use Data::Dumper;

print "The automated system is in full command of an advanced turret, let's hope the programmer didn't hold any grudges\n";

my $port = Device::SerialPort->new("/dev/ttyACM0");
$port->databits(8);
$port->baudrate(9600);
$port->parity("none");
$port->stopbits(1);
my $pitch = 90;
my $yaw = 90;
my $flywheel = 0;

print "Beginning command loop\n";
commandLoop();

# subroutine declarations

sub commandLoop{
    sleep(10); #wait for video feed
    # Target variables
    my $shotsRemaining = 15;
    my $expectedTargets = 3;
    my $shotsPerTarget = 5;
    # Other ones
    my $margin = 10;
    my $lastTargetLoc = {x=>0,y=>0};
    my $isLost = 0;
    ReadMode('cbreak');
    $flywheel = 1;
    print "Start up the flywheel\n";
    sendSerial("s");
    while ($shotsRemaining > 0) {
        my @targets = readTargets();
        if(scalar @targets != 4){
            if(!$isLost){
                print STDERR "lost\n";
                $isLost = 1;
            }
            sleepMs(250);
        }else{
            if($isLost){
                print STDERR "active\n";
                $isLost = 0;
            }
            #print "found 3 targets:\n";
            #foreach my $t (@targets){
            #    print $t->{x} . ", " . $t->{y} . "\n";
            #}
            my $targetIndex = (($expectedTargets - 1) - int(($shotsRemaining - 1) / $shotsPerTarget)) + 1;
            my $target = $targets[$targetIndex];
            print "target selected: " . $target->{x} . ", " . $target->{y} . "\n";
            if(withinMargin($margin, $target, $targets[0])){
                print "target found, stabalizing\n";
                # stabalize
                if(withinMargin($margin, $target, $lastTargetLoc)){
                    print "firing\n";
                    fire();
                    $shotsRemaining--;
                }else{
                    $lastTargetLoc = $target;
                    sleepMs(1000); # wait to verify it is actually stable
                }
            }else{
                adjustAim($targets[0], $target);
                sleepMs(2000);
            }
        }
    }
    $flywheel = 0;
    sendSerial("h");
    print STDERR "shutdown\n";
    ReadMode('normal'); 
}

sub fire {
    if(!$flywheel){
        print STDERR "error\n"
    }else {
        sendSerial("f")
    }
}

sub adjustAim {
    #print STDERR "search\n";
    #print "Aiming\n";
    my ($c, $t) = @_;
    my $vidToServoRatio = 25;
    #print Dumper(@_);
    my $xDiff = $t->{x} - $c->{x};
    my $yDiff = $t->{y} - $c->{y};
    #print "xDiff: $xDiff\nyDiff: $yDiff\nxDiffR:".$xDiff/$vidToServoRatio."\nyDiffR:".$yDiff/$vidToServoRatio."\n";
    $pitch += roundServo(($yDiff / $vidToServoRatio));
    $pitch = checkBound($pitch, 76,120);
    $yaw -= roundServo(($xDiff / $vidToServoRatio));
    $yaw = checkBound($yaw, 65,115);
    sendSerial("${pitch}p");
    sendSerial("${yaw}y");
}

sub roundServo {
    my ($x) = @_;
    if ($x < 0 && $x > -1){
        return -1;
    } elsif ($x > 0 && $x < 1){
        return 1;
    } else {
        return int($x);
    }
}

sub checkBound {
    my ($x, $min, $max) = @_;
    if($x < $min){
        return int($min);
    } elsif ($x > $max) {
        return int($max);
    }else{
        return int($x);
    }
}

sub withinMargin {
    my ($margin, $a, $b) = @_;
    my $xDiff = abs($a->{x} - $b->{x});
    my $yDiff = abs($a->{y} - $b->{y});
    print "Margin: x $xDiff y $yDiff\n";
    if($xDiff < $margin && $yDiff < $margin){
        return 1;
    } else {
        return 0;
    }
}

sub readTargets{
    #print "Reading targets\n";
    my @targets = ();
    my $line;
    sysread(STDIN, $line, 4096); # read 4096 bytes
    $line = (split /\n/, $line)[-1];
    if(!defined($line)){ 
        return @targets;
    }
    my @nums = split(/ /, $line);
    for(my $i = 0; $i < scalar @nums; $i += 2){
        if(length $nums[$i] && length $nums[$i + 1]){
            $targets[$i / 2]{x} = $nums[$i];
            $targets[$i / 2]{y} = $nums[$i + 1];
        }else{
            print "Invalid coordinates found\n";
        }
    }
    my $center = shift(@targets);
    @targets = sort {$a->{x} <=> $b->{x}} @targets;
    unshift(@targets, $center);
    @targets = adjustForDistance(@targets);
    #print Dumper(@targets);
    return @targets;
}

sub adjustForDistance {
    my (@targets) = @_;
    $targets[1]{y} -= 50; # left target is far, aim up
    $targets[2]{y} += 25; # center target is close, aim low
    $targets[3]{y} += 0; # right target is medium, aim center
    return @targets;
}

sub sendSerial{
    my ($command) = @_;
    #send serial to arduino
    $port->write($command);
    print "SERIAL: $command\n";
}

sub sleepMs{
    my ($ms) = @_;
    select(undef, undef, undef, $ms / 1000);
}

